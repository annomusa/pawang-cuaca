apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'
apply plugin: 'com.fernandocejas.frodo'

frodo {
    enabled = true
}

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
    buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")

    defaultConfig {
        minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")

        applicationId globalConfiguration.getAt("androidApplicationId")
        versionCode globalConfiguration.getAt("androidVersionCode")
        versionName globalConfiguration.getAt("androidVersionName")
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def appDependencies = rootProject.ext.presentationDependencies

    compile project(':domain')
    compile project(':data')

    compile appDependencies.appCompat
    compile appDependencies.design
    compile appDependencies.recyclerView
    compile appDependencies.cardView

    // GMS
    compile appDependencies.gms
    compile appDependencies.gmsLocation

    // Retrofit
    compile appDependencies.gson
    compile appDependencies.retrofit
    compile appDependencies.retrofitConverter
    compile appDependencies.retrofitAdapterRx

    // RxJava
    compile appDependencies.rxJava
    compile appDependencies.rxAndroid

    // ButterKnife
    compile appDependencies.butterKnife

    // Dagger2
    compile appDependencies.dagger
    apt appDependencies.daggerCompiler
    provided appDependencies.javaxAnnotation

    // Realm Adapter
    compile appDependencies.realmDbAdapter

    // Picasso
    compile appDependencies.picasso
}

retrolambda {
    jvmArgs '-noverify'
}

